// Free to modify variables
num leafRadius = 3 + randomInt(4);
num trunkHeight = 10 + randomInt(7);
num leafTopHeight = trunkHeight * 1.3;
num leafBottomHeight = 1 + randomInt(8);
str trunkBlockX = "minecraft:spruce_log[axis=x]";
str trunkBlockY = "minecraft:spruce_log[axis=y]";
str trunkBlockZ = "minecraft:spruce_log[axis=z]";
str leafBlock   = "minecraft:spruce_leaves[distance=1,persistent=false]";

num leafMidPointRatio = 0.1;
num leafMidHeight = leafBottomHeight + (leafTopHeight-leafBottomHeight) * leafMidPointRatio;
for (num y = 0; y <= leafTopHeight; y = y + 1) {
    num radius = 0;
    if (y < leafMidHeight) {
        radius = (-pow((y-leafMidHeight)/(leafBottomHeight-leafMidHeight),2)+1)*leafRadius;
    } else {
        radius = (-pow((y-leafMidHeight)/(leafTopHeight-leafMidHeight),2)+1)*leafRadius;
    }
    if (y >= leafBottomHeight) {

        if (radius <= 1) { // Skip generating circle if radius is less than 1
            block(0, y, 0, leafBlock);
        } else {
            num squareradius = radius * radius; // Square the radius to avoid unnecessary square root when comparing dist
            for (num x = round(-radius); x <= round(radius); x = x + 1) {
                for (num z = round(-radius); z <= round(radius); z = z + 1) {
                    num squaredist = x*x+z*z;
                    if ((squaredist <= squareradius && sin(3.1415926 * y) > 0) || squaredist <= squareradius / 3) {
                        if (squareradius - squaredist < squareradius * 0.4) { // If close to edge
                            if (randomInt(2) == 0) {
                                block(x, y, z, leafBlock, false);
                            }
                        } else {
                            block(x, y, z, leafBlock, false);
                        }
                    }
                }
            }
        }
    }

    if (y <= trunkHeight) {
        block(0, y, 0, trunkBlockY); // Main trunk
    }
}

block(0, -1, 0, "minecraft:dirt");

// --- Random sticks around base ---
// Config: max sticks and rarity control (higher = rarer)
num maxSticks = 4;
num attempts = 40;           // how many placement tries to find up to maxSticks
num baseRadius = leafRadius; // search radius around trunk at ground level
num rareScale = 5;           // higher number = rarer overall spawns

num placed = 0;
for (num t = 0; t < attempts && placed <= maxSticks; t = t + 1) {
    // Sample a candidate position with distance bias toward trunk.
    // Use "rejection sampling": pick within square, reject outside circle sometimes.
    num x = randomInt(baseRadius * 2 + 1) - baseRadius; // [-R, R]
    num z = randomInt(baseRadius * 2 + 1) - baseRadius;

    // Skip the trunk center
    if (x == 0 && z == 0) continue;

    // Compute squared distance and bias probability:
    // Closer to center => higher chance. Farther => lower chance.
    num d2 = x * x + z * z;
    num r2 = baseRadius * baseRadius;
    if (d2 > r2) continue;

    // Distance bias: probNear ranges from ~1 near center to small near edge
    // Using a smooth falloff curve (quadratic)
    num probNearScaled = (r2 - d2) / r2;          // 1.0 at center, ~0 at edge
    num probPermille = floor(probNearScaled * 1000); // 0..1000

    // Add overall rarity gate
    // Effective chance ~ probNearScaled / rareScale
    if (randomInt(rareScale * 1000) >= probPermille) continue;

    if (structure(x, 0, z, "ground_stick", "NONE")) {
        placed = placed + 1;
    }
}