num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;
num twopi = 6.28318530718;

num trunkHeight = 4+randomInt(20);
str trunkBlock = "minecraft:jungle_log";
str rootBlock = "minecraft:jungle_wood";

num sideBranchInterval = 4+randomInt(2);
num sideBranchMinHeight = 3+randomInt(3);
num sideBranchLengthMin = 4+randomInt(2);
num sideBranchLengthVariation = 2;
num sideBranchVerticalVariation = 0;
num sideBranchVerticalDirection = 0.5+randomInt(randPrecision)/randPrecision/2;
num sideBranchLeafRadiusMin = 6;
num sideBranchLeafTranslate = 0;
num sideBranchLeafTopFlatness = 4;
num sideBranchLeafBottomFlatness = 6;
num sideBranchLeafDensity = 0.7;
str sideBranchLeafBlock = "minecraft:jungle_leaves[distance=1,persistent=false]";
str sideBranchBlockX = "minecraft:jungle_log[axis=x]";
str sideBranchBlockY = "minecraft:jungle_log[axis=y]";
str sideBranchBlockZ = "minecraft:jungle_log[axis=z]";

num topBranches = 2+randomInt(2);
num topBranchLengthUpwardsMin = 7+randomInt(3);
num topBranchLengthOutwardsMin = 4+randomInt(3);
num topBranchVerticalDirection = 1;
num topBranchLeafRadiusMin = 6;
num topBranchLeafTranslate = 0;
num topBranchLeafTopFlatness = 4;
num topBranchLeafBottomFlatness = 6;
num topBranchLeafDensity = 0.7;
str topBranchLeafBlock = "minecraft:jungle_leaves[distance=1,persistent=false]";
str topBranchBlockX = "minecraft:jungle_log[axis=x]";
str topBranchBlockY = "minecraft:jungle_log[axis=y]";
str topBranchBlockZ = "minecraft:jungle_log[axis=z]";

// cocoa
num cocoaRoll = randomInt(10); // 1 in 10 chance
if (cocoaRoll == 0) {
structure(0, 1, 0, "cocoa_bean", "NONE");
structure(0, 2, 0, "cocoa_bean", "NONE");
structure(0, 3, 0, "cocoa_bean", "NONE");
}


// Generate roots at base of trunk
num roots = 1+randomInt(3);
num rootMaxDepth = 3;
num rootHeight = 5;
num rootLength = 2;
num rootAngle = randomInt(randPrecision)/randPrecision*2*pi;
for (num r = 0; r < roots; r = r + 1) {
    num dx = sin(rootAngle);
    num dz = cos(rootAngle);
    for (num l = 0; l <= rootLength; l = l + 1) {
        num height = rootHeight * (1-(l/rootLength));
        for (num h = height; h >= -rootMaxDepth; h = h - 1) {
            if (check(round(l*dx),round(h),round(l*dz)) != "AIR") break;
            block(l*dx, h, l*dz, rootBlock);
        }
    }
    rootAngle = rootAngle + goldenAngle;
}

num branchAngle = randomInt(randPrecision)/randPrecision*2*pi;
for (num i = 0; i < trunkHeight; i = i + 1) {
    if (i > sideBranchMinHeight) {
        if (i % sideBranchInterval == 0) {
            // Branch direction vector
            num dx = sin(branchAngle);
            num dy = sideBranchVerticalDirection + randomInt(randPrecision)/randPrecision*sideBranchVerticalVariation;
            num dz = cos(branchAngle);
            // Normalize vector
            num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
            dx = dx/mag;
            dy = dy/mag;
            dz = dz/mag;
            // Branch origin
            num ox = 0;
            num oy = i;
            num oz = 0;
            // Set branch block based on largest unsigned vector component
            str branchBlock = sideBranchBlockY;
            if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = sideBranchBlockX;
            else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = sideBranchBlockZ;
            num branchLength = sideBranchLengthMin + randomInt(randPrecision)/randPrecision*sideBranchLengthVariation;
            // Generate branch
            for (num l = 0; l <= branchLength; l = l + 1) {
                block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
            }
            // Rotate angle of next branch
            branchAngle = branchAngle + goldenAngle;
            // Generate leaf cluster at the end of the branch
            num radius = sideBranchLeafRadiusMin;
            num radiusSquared = pow(radius, 2);
            // Leaf cluster origin (end of branch)
            num lox = ox+branchLength*dx;
            num loy = oy+branchLength*dy + sideBranchLeafTranslate;
            num loz = oz+branchLength*dz;
            for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
                for (num cy = ceil(-radius/sideBranchLeafBottomFlatness); cy <= ceil(radius/sideBranchLeafTopFlatness); cy = cy + 1) {
                    for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                        num squashFactor = sideBranchLeafBottomFlatness;
                        if (cy > 0) squashFactor = sideBranchLeafTopFlatness;
                        num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                        if (distanceSquared < radiusSquared && randomInt(randPrecision)/randPrecision < sideBranchLeafDensity) {
                            block(lox+cx, loy+cy, loz+cz, sideBranchLeafBlock, false);
                        }
                    }
                }
            }
        }
    }
    block(0, i, 0, trunkBlock);
}

for (num b = 1; b <= topBranches; b = b + 1) {
    num t = b/topBranches;
    num ti = 1-t;
    // Branch direction vector
    num dx = sin(branchAngle) * t;
    num dy = topBranchVerticalDirection;
    num dz = cos(branchAngle) * t;
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;
    // Branch origin
    num ox = 0;
    num oy = trunkHeight;
    num oz = 0;
    // Set branch block based on largest unsigned vector component
    str branchBlock = topBranchBlockY;
    if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = topBranchBlockX;
    else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = topBranchBlockZ;
    num branchLength = topBranchLengthUpwardsMin*ti + topBranchLengthOutwardsMin*t;
    // Generate branch
    for (num l = 0; l <= branchLength; l = l + 1) {
        block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
    }
    // Rotate angle of next branch
    branchAngle = branchAngle + goldenAngle;
    // Generate leaf cluster at the end of the branch
    num radius = topBranchLeafRadiusMin;
    num radiusSquared = pow(radius, 2);
    // Leaf cluster origin (end of branch)
    num lox = ox+branchLength*dx;
    num loy = oy+branchLength*dy + topBranchLeafTranslate;
    num loz = oz+branchLength*dz;
    for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
        for (num cy = ceil(-radius/topBranchLeafBottomFlatness); cy <= ceil(radius/topBranchLeafTopFlatness); cy = cy + 1) {
            for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                num squashFactor = topBranchLeafBottomFlatness;
                if (cy > 0) squashFactor = topBranchLeafTopFlatness;
                num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                if (distanceSquared < radiusSquared && randomInt(randPrecision)/randPrecision < topBranchLeafDensity) {
                    block(lox+cx, loy+cy, loz+cz, topBranchLeafBlock, false);
                }
            }
        }
    }
}

for (num v = 35; v > trunkHeight; v = v - 1) {
    num rx = sin(randomInt(randPrecision)/randPrecision * twopi) * (randomInt(9) + 3);
    num rz = cos(randomInt(randPrecision)/randPrecision * twopi) * (randomInt(9) + 3);
    structure(rx, v, rz, "place_vines", "NONE");
    structure(-rx, v, rz, "place_vines", "NONE");
    structure(rx, v, -rz, "place_vines", "NONE");
    structure(-rx, v, -rz, "place_vines", "NONE");
}

block(0, -1, 0, "minecraft:dirt");

// --- Random sticks around base ---
// Config: max sticks and rarity control (higher = rarer)
num maxSticks = 4;
num attempts = 40;           // how many placement tries to find up to maxSticks
num baseRadius = 8; // search radius around trunk at ground level
num rareScale = 5;           // higher number = rarer overall spawns

num placed = 0;
for (num t = 0; t < attempts && placed <= maxSticks; t = t + 1) {
    // Sample a candidate position with distance bias toward trunk.
    // Use "rejection sampling": pick within square, reject outside circle sometimes.
    num x = randomInt(baseRadius * 2 + 1) - baseRadius; // [-R, R]
    num z = randomInt(baseRadius * 2 + 1) - baseRadius;

    // Skip the trunk center
    if (x == 0 && z == 0) continue;

    // Compute squared distance and bias probability:
    // Closer to center => higher chance. Farther => lower chance.
    num d2 = x * x + z * z;
    num r2 = baseRadius * baseRadius;
    if (d2 > r2) continue;

    // Distance bias: probNear ranges from ~1 near center to small near edge
    // Using a smooth falloff curve (quadratic)
    num probNearScaled = (r2 - d2) / r2;          // 1.0 at center, ~0 at edge
    num probPermille = floor(probNearScaled * 1000); // 0..1000

    // Add overall rarity gate
    // Effective chance ~ probNearScaled / rareScale
    if (randomInt(rareScale * 1000) >= probPermille) continue;

    if (structure(x, 0, z, "ground_stick", "NONE")) {
        placed = placed + 1;
    }
}